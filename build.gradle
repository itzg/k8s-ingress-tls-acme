plugins {
    id 'org.springframework.boot' version '3.1.2'
    id 'io.spring.dependency-management' version '1.1.2'
    id 'io.freefair.lombok' version '8.1.0'
    id 'java'
    id 'io.github.itzg.simple-boot-image' version '3.0.0'
    // https://github.com/qoomon/gradle-git-versioning-plugin
    id 'me.qoomon.git-versioning' version '6.4.2'
}

group = 'io.github.itzg'
version = '0.0.1-SNAPSHOT'
gitVersioning.apply {
    refs {
        tag('.+') {
            version = '${ref}'
        }
        branch('main') {
            version = '${describe.tag}-SNAPSHOT'
        }
        branch('.+') {
            version = '${ref}-SNAPSHOT'
        }
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_18
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'io.fabric8:kubernetes-client:6.8.0'
    implementation 'com.nimbusds:nimbus-jose-jwt:9.31'
    implementation 'org.bouncycastle:bcpkix-jdk18on:1.76'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
}

tasks.named('test') {
    useJUnitPlatform()
}

simpleBootImage {
    baseImage = 'eclipse-temurin:18'
}